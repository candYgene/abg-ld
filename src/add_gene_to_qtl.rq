--
-- SPARQL query to add genes that overlap with QTLs
-- associated with the following traits:
--   'nipple quantity'		- http://purl.obolibrary.org/obo/VT_1000206
--   'teat number'			- http://purl.obolibrary.org/obo/CMO_0000445
--   'teat number, left'  	- http://purl.obolibrary.org/obo/CMO_0000472
--   'teat number, right' 	- http://purl.obolibrary.org/obo/CMO_0000473
--
-- Prerequisite: GRANT SPARQL_UPDATE to "SPARQL";
--

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>

INSERT INTO <http://www.animalgenome.org/QTLdb/pig> {
   ?qtl obo:SO_overlaps ?gene
}
WHERE {
   GRAPH <http://www.animalgenome.org/QTLdb/pig> {
      ?qtl a obo:SO_0000771 ;
         faldo:location ?loc ;
         obo:RO_0002610 ?trait .
      ?loc faldo:begin ?begin ;
         faldo:end ?end .
      ?begin faldo:reference ?chr ;
         faldo:position ?begin_pos .
      ?end faldo:position ?end_pos .
      ?chr owl:sameAs ?chr2 .
   }
   GRAPH <http://ensembl.org/Sus_scrofa> {
      ?loc2 ?p ?chr2 ;
         faldo:begin ?begin2 ;
         faldo:end ?end2 ;
         ^faldo:location ?gene .
      ?gene a obo:SO_0001217 .
      ?begin2 faldo:position ?begin_pos2 .
      ?end2 faldo:position ?end_pos2 .
   }
   FILTER(?trait IN (obo:VT_1000206, obo:CMO_0000445, obo:CMO_0000472, obo:CMO_0000473)) .
   FILTER((xsd:integer(?begin_pos) > xsd:integer(?begin_pos2) &&
           xsd:integer(?begin_pos) < xsd:integer(?end_pos2)) ||
          (xsd:integer(?end_pos) > xsd:integer(?begin_pos2) &&
           xsd:integer(?end_pos) < xsd:integer(?end_pos2)) ||
          (xsd:integer(?begin_pos) < xsd:integer(?begin_pos2) &&
          xsd:integer(?end_pos) > xsd:integer(?end_pos2)) ||
          xsd:integer(?begin_pos) > xsd:integer(?begin_pos2) &&
          xsd:integer(?end_pos) < xsd:integer(?end_pos2))
}

--
-- SPARQL query to delete above triples
--
-- PREFIX obo: <http://purl.obolibrary.org/obo/>
-- WITH  <http://www.animalgenome.org/QTLdb/pig>
-- DELETE { ?s ?p ?o }
-- { ?s ?p ?o ; obo:SO_overlaps ?o }
